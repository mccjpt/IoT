_vcsCheckOut()
/* Makes a new repository as a copy of an older version and starts a new MetaEdit+ to log into it, without affecting the VCS, this MetaEdit+ or this repository */

/* Set paths for executables and versioning target */
_vcsPaths()

$newDBName = __(prompt 
	'New MetaEdit+ repository name (not ' 
	@dbNameNow = __(internal 'dbName' read)
	@dbDirNow = __(internal 'dbDir' read)
	@dbNameNow 
	if not (@dbDirNow = @dbNameNow or @dbDirNow = __($dbBaseDir sep @dbNameNow)) then 
		@dbNameNow ', ' 
		if @dbDirNow =~ __('*' sep '*') then 'tail of ' endif
		@dbDirNow 
	endif
	' or ' $dbBackupName ')' 
	ask)
$newDBDir = __($dbBaseDir sep $newDBName)

/* Open the check-out script file for writing... */
filename $checkOutScript write
_vcsHeader()
if $vcsExeDir then 'SET PATH=%PATH%;' $vcsExeDir newline endif 
'if not exist "' $newDBDir '" goto newDBOK
call :pause DANGER: ' $newDBDir ' already exists, about to delete - press Enter to continue, or press Ctrl+C and answer yes to abort
rd /s /q "' $newDBDir '"

:newDBOK
pushd "' $vcsWorkingDir '"
'
subreport '_vcsCheckOut_' $vcs arguments $newDBDir run
'
popd
"' $metaeditExe '" fileInPatches'

/* ...and close the script file */
close

/* Execute the generated script file */
external $checkOutScript execute
